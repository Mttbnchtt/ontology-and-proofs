# extract exactly all concepts related to a proof 
# either directly via the "contains_concept" relation
# or indirectly because they are concepts contained in a relation or operation related to the proof.

select distinct
    ?main_item_iri
    ?main_item_label
    ?concept_iri
    ?concept_label
where {
    {
        ?main_item_iri 
            a <https://www.foom.com/core#proof> ;
            rdfs:label  ?main_item_label ;
            <https://www.foom.com/core#is_in> <https://www.foom.com/core#document__elements_book_1> ;
            <https://www.foom.com/core#contains_concept> ?concept_iri .
        ?concept_iri
            rdfs:label ?concept_label .
    }
    UNION
    {
        ?main_item_iri 
            a <https://www.foom.com/core#proof> ;
            rdfs:label  ?main_item_label ;
            <https://www.foom.com/core#is_in> <https://www.foom.com/core#document__elements_book_1> ;
            <https://www.foom.com/core#refers_to> ?item_iri . # relation or operation related to the proof
        ?item_iri
            <https://www.foom.com/core#contains_concept> ?concept_iri . # concept contained in the relation or operation related to the proof
        ?concept_iri
            rdfs:label ?concept_label .
    }
}
order by
    ?main_item_iri
