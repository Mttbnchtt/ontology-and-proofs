# link weights for the elements of the class Common notion

SELECT
    ?s ?o
    (count (*) as ?links)
WHERE {
    ?s a <https://www.foom.com/core#common_notion> .
    { ?s <https://www.foom.com/core#has_statement> ?o .}
    union
    { ?s <https://www.foom.com/core#has_statement> 
            / <https://www.foom.com/core#refers_to> ?o . } # refers to
    union
    { ?s <https://www.foom.com/core#has_statement> 
            / <https://www.foom.com/core#refers_to> 
            / <https://www.foom.com/core#contains_concept> ?o . } # refers to / contains concept
    ######################
    union
    { ?s <https://www.foom.com/core#has_statement> 
            / <https://www.foom.com/core#refers_to> 
            / <https://www.foom.com/core#has_range> ?o . } # refers to / range
    union
    { ?s <https://www.foom.com/core#has_statement> 
            / <https://www.foom.com/core#refers_to> 
            / <https://www.foom.com/core#has_range>
            / <https://www.foom.com/core#contains_concept>  ?o . } # refers to / range / contains concept
    #####################
    union
    { ?s <https://www.foom.com/core#has_statement> 
            / <https://www.foom.com/core#refers_to> 
            / <https://www.foom.com/core#has_domain> ?o . } # refers to / domain
    union
    { ?s <https://www.foom.com/core#has_statement> 
            / <https://www.foom.com/core#refers_to> 
            / <https://www.foom.com/core#has_domain>
            / <https://www.foom.com/core#contains_concept>  ?o . } # refers to / domain / contains concept
}
group by ?s ?o
order by ?s desc(?links) 